# Copyright 2016 MongoDB Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

message(WARNING "This CMake target is deprecated.  Use 'mongo::mongocxx_static' instead.  Consult the example projects for further details.")

set(LIBMONGOCXX_STATIC_VERSION_MAJOR @MONGOCXX_VERSION_MAJOR@)
set(LIBMONGOCXX_STATIC_VERSION_MINOR @MONGOCXX_VERSION_MINOR@)
set(LIBMONGOCXX_STATIC_VERSION_PATCH @MONGOCXX_VERSION_PATCH@)
set(LIBMONGOCXX_STATIC_PACKAGE_VERSION @MONGOCXX_VERSION@)

find_package(libbsoncxx-static @MONGOCXX_VERSION_NO_EXTRA@ REQUIRED)

# We need to pull in the libmongoc-static-* library to read the MONGOC_STATIC_LIBRARIES variable.
# We can ignore the other variables exported by that package (e.g. MONGOC_STATIC_INCLUDE_DIRS,
# MONGOC_STATIC_DEFINITIONS), since mongocxx hides the existence of libmongoc from the user through
# abstraction.  mongocxx users generally should not need to include libmongoc headers directly.
find_package(libmongoc-static-1.0 @LIBMONGOC_REQUIRED_VERSION@ REQUIRED)

@PACKAGE_INIT@

set(LIBMONGOCXX_STATIC_INCLUDE_DIRS "")
foreach(_INCLUDE_DIR @PACKAGE_INCLUDE_INSTALL_DIRS@)
    set_and_check(_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/${_INCLUDE_DIR}")
    list(APPEND LIBMONGOCXX_STATIC_INCLUDE_DIRS "${_INCLUDE_DIR}")
endforeach()
list(APPEND LIBMONGOCXX_STATIC_INCLUDE_DIRS ${LIBBSONCXX_STATIC_INCLUDE_DIRS})

# We want to provide an absolute path to the library and we know the directory and the base name,
# but not the suffix, so we use CMake's find_library() to pick that up.
find_library(LIBMONGOCXX_STATIC_LIBRARY_PATH mongocxx-static PATHS "${PACKAGE_PREFIX_DIR}/@PACKAGE_LIBRARY_INSTALL_DIRS@" NO_DEFAULT_PATH)
set(LIBMONGOCXX_STATIC_LIBRARIES ${LIBMONGOCXX_STATIC_LIBRARY_PATH} ${LIBBSONCXX_STATIC_LIBRARIES} ${MONGOC_STATIC_LIBRARIES})

set(LIBMONGOCXX_STATIC_DEFINITIONS MONGOCXX_STATIC ${LIBBSONCXX_STATIC_DEFINITIONS})
